
Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.Продажи.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПродажаТоваровСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	ПродажаТоваровСписокНоменклатуры.Характеристика КАК Характеристика,
	|	СУММА(ПродажаТоваровСписокНоменклатуры.Количество) КАК Количество,
	|	СУММА(ПродажаТоваровСписокНоменклатуры.Сумма) КАК Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ПродажаТоваров.СписокНоменклатуры КАК ПродажаТоваровСписокНоменклатуры
	|ГДЕ
	|	ПродажаТоваровСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажаТоваровСписокНоменклатуры.Номенклатура,
	|	ПродажаТоваровСписокНоменклатуры.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	ПРЕДСТАВЛЕНИЕ(Товары.Номенклатура.Наименование) КАК НоменклатураП,
	|	Товары.Количество КАК Количество,
	|	Товары.Сумма КАК Сумма,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				(Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура КАК Номенклатура,
	|						Товары.Характеристика КАК Характеристика
	|					ИЗ
	|						Товары КАК Товары)) КАК ОстаткиНоменклатурыОстатки
	|		ПО Товары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|			И Товары.Характеристика = ОстаткиНоменклатурыОстатки.Характеристика";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Отказ = Истина;
			
			Сообщить("Мало товара " + Выборка.НоменклатураП 
				+ ", из необходимых " + Выборка.Количество + " в наличии имеется только " 
				+ Выборка.КоличествоОстаток);
			КонецЕсли;
			
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Характеристика = Выборка.Характеристика;
		Движение.Количество = Выборка.Количество;
		
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Характеристика = Выборка.Характеристика;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Выборка.Сумма;
	КонецЦикла; 
	
КонецПроцедуры
